10 SCREEN 9
20 W = 640
30 H = 350
40 CX = W / 2
50 CY = H / 2
60 BLACK = 0

70 BLUE = 1
80 GREEN = 2
90 CYAN = 3
100 RED = 4
110 MAGENTA = 5
120 BROWN = 6
130 GREY = 7
140 DARKGREY = 8

150 LIGHTBLUE = BLUE + 8 ' 9
160 LIGHTGREEN = GREEN + 8 ' 10
170 LIGHTCYAN = CYAN + 8 ' 11
180 LIGHTRED = RED + 8 ' 12
190 LIGHTMAGENTA = MAGENTA + 8 ' 13
200 YELLOW = BROWN + 8 ' 14
210 WHITE = GREY + 8 ' 15

220 PI = 3.14159265
230 OFFSET = 40

240 ' START DRAWING the jukebox. Inspired by https://creazilla.com/media/clipart/1723218/jukebox
250 CLS
260 PAINT (CX, CY), RED

270 ' Draw the three arcs of the jukebox frame
280 R1 = 120
290 R2 = 110
300 R3 = 100
310 CIRCLE (CX, CY - OFFSET), R1, BLACK, 2 * PI, PI, 1
320 CIRCLE (CX, CY - OFFSET), R2, BLACK, 2 * PI, PI, 1
330 CIRCLE (CX, CY - OFFSET), R3, BLACK, 2 * PI, PI, 1

340 ' Draw boxes below the arcs of the jukebox frame, three shorter and two taller but narrower in between
350 BOXH1 = 4
360 BOXH2 = 6
370 FOR I = 0 TO 4
380   IF I MOD 2 = 0 THEN GOSUB 1988 ELSE GOSUB 1994
410 NEXT I
420 BOXH12 = 24 ' 24 IS THE SUM OF THE HEIGHT OF THE PREVIOUS 5 BOXES
430 FRAMEY = CY - OFFSET + BOXH12
440 BOXH3 = 100
450 LINE (CX - R1, FRAMEY)-(CX - R2, FRAMEY + BOXH3), BLACK, B
460 LINE (CX - R2, FRAMEY)-(CX - R3, FRAMEY + BOXH3), BLACK, B
470 LINE (CX + R1, FRAMEY)-(CX + R2, FRAMEY + BOXH3), BLACK, B
480 LINE (CX + R2, FRAMEY)-(CX + R3, FRAMEY + BOXH3), BLACK, B
490 FRAMEY = FRAMEY + BOXH3
400 BOXH4 = 40
510 LINE (CX - R1 - 2, FRAMEY)-(CX - R3 + 2, FRAMEY + BOXH4), BLACK, B
520 LINE (CX + R1 + 2, FRAMEY)-(CX + R3 - 2, FRAMEY + BOXH4), BLACK, B
530 FRAMEY = FRAMEY + BOXH4
540 BOXH5 = 26
550 BOXH6 = 14
560 LINE (CX - R1, FRAMEY)-(CX - R3, FRAMEY + BOXH5), BLACK, B
570 LINE (CX + R1, FRAMEY)-(CX + R3, FRAMEY + BOXH5), BLACK, B
580 LINE (CX - R3, FRAMEY)-(CX + R3, FRAMEY + BOXH6), BLACK, B

590 ' Draw the arc of the jukebox window
600 R4 = 80
610 CIRCLE (CX, CY - OFFSET - 10), R4, BLACK, 2 * PI, PI, 1
620 LINE (CX - R4, CY - OFFSET - 10)-(CX + R4, CY - OFFSET -10), BLACK

630 ' Draw horizontal box below the jukebox window
640 W1 = 6
650 LINE (CX - R4 - (W1 / 2), CY - OFFSET - 2)-(CX - R4 + (W1 / 2), CY - OFFSET + BOXH12 + 2), BLACK, B
660 LINE (CX - (W1 / 2), CY - OFFSET - 2)-(CX + (W1 / 2), CY - OFFSET + BOXH12 + 2), BLACK, B
670 LINE (CX + R4 - (W1 / 2), CY - OFFSET - 2)-(CX + R4 + (W1 / 2), CY - OFFSET + BOXH12 + 2), BLACK, B
680 LINE (CX - R4 + (W1 / 2), CY - OFFSET)-(CX - (W1 / 2), CY - OFFSET + BOXH12), BLACK, B
690 LINE (CX + (W1 / 2), CY - OFFSET)-(CX + R4 - (W1 / 2), CY - OFFSET + BOXH12), BLACK, B

700 ' Draw button panel and buttons
710 BTNPANELY = CY - OFFSET + BOXH12 + 4
720 BTNPANELW = R4 * 2 ' 80 * 2 = 160
730 DISPLAYH = 9
740 LINE (CX - R4, BTNPANELY)-(CX + R4, BTNPANELY + BOXH12), BLACK, B ' BUTTON PANEL
750 LINE (CX - 34, BTNPANELY + 3)-(CX + 34, BTNPANELY + 3 + DISPLAYH), BLACK, B ' DISPLAY
760 BTNR = 4
770 BTND = ((BTNPANELW - BTNR) / 10)
780 FOR I = -4 TO 4
790   CIRCLE(CX + (BTND * I), BTNPANELY + 3 + DISPLAYH + BTNR + 2), BTNR, BLACK,,,1
800 NEXT I

810 ' Draw the speaker area
812 SPEAKERY = BTNPANELY + BOXH12
814 BOXH7 = 50
817 R6 = 50
818 R7 = 30
819 LINE (CX - R6 - 3, SPEAKERY)-(CX - R7 - 3, SPEAKERY + BOXH7), BLACK, B ' LEFT LARGE BOX
820 LINE (CX + R7 + 3, SPEAKERY)-(CX + R6 + 3, SPEAKERY + BOXH7), BLACK, B ' RIGHT LARGE BOX
822 LINE (CX - R6 - 5, SPEAKERY + BOXH7)-(CX - R7 - 1, SPEAKERY + BOXH7 + 6), BLACK, B ' LEFT SMALL BOX
824 LINE (CX + R7 + 1, SPEAKERY + BOXH7)-(CX + R6 + 5, SPEAKERY + BOXH7 + 6), BLACK, B ' RIGHT SMALL BOX
827 CIRCLE (CX - 3, SPEAKERY + BOXH7 + 6), R6, BLACK, PI, 3 * (PI / 2), 1 ' BOTTOM LEFT OUTER ARC
828 CIRCLE (CX - 3, SPEAKERY + BOXH7 + 6), R7, BLACK, PI, 3 * (PI / 2), 1 ' BOTTOM LEFT INNER ARC
830 CIRCLE (CX + 3, SPEAKERY + BOXH7 + 6), R6, BLACK, 3 * (PI / 2), 2 * PI, 1 ' BOTTOM RIGHT OUTER ARC
832 CIRCLE (CX + 3, SPEAKERY + BOXH7 + 6), R7, BLACK, 3 * (PI / 2), 2 * PI, 1 ' BOTTOM RIGHT INNER ARC
834 LINE (CX - 3, SPEAKERY + BOXH7 + 6 + R7 - 2)-(CX + 3, SPEAKERY + BOXH7 + 6 + R6 + 2), BLACK, B ' BUTTOM SMALL BOX

900 ' Paint by number!
901 PAINT (CX, CY - OFFSET - R1 + 2), LIGHTRED, BLACK ' OUTER ARC
902 PAINT (CX, CY - OFFSET - R2 + 2), YELLOW, BLACK ' INNER ARC
903 PAINT (CX, CY - OFFSET - R3 + 2), BLACK, BLACK ' JUKEBOX PANEL
904 PAINT (CX, CY - OFFSET - R4 + 2), LIGHTCYAN, BLACK ' JUKEBOX WINDOW
905 PAINT (CX - R2, CY - OFFSET + 2), GREY, BLACK
906 PAINT (CX + R2, CY - OFFSET + 2), GREY, BLACK
907 PAINT (CX - R2, CY - OFFSET + BOXH1 + 2), LIGHTRED, BLACK
908 PAINT (CX + R2, CY - OFFSET + BOXH1 + 2), LIGHTRED, BLACK
909 PAINT (CX - R2, CY - OFFSET + BOXH1 + BOXH2 + 2), GREY, BLACK
910 PAINT (CX + R2, CY - OFFSET + BOXH1 + BOXH2 + 2), GREY, BLACK
911 PAINT (CX - R2, CY - OFFSET + (BOXH1 * 2) + BOXH2 + 2), LIGHTRED, BLACK
912 PAINT (CX + R2, CY - OFFSET + (BOXH1 * 2) + BOXH2 + 2), LIGHTRED, BLACK
913 PAINT (CX - R2, CY - OFFSET + (BOXH1 * 2) + (BOXH2 * 2) + 2), GREY, BLACK
914 PAINT (CX + R2, CY - OFFSET + (BOXH1 * 2) + (BOXH2 * 2) + 2), GREY, BLACK
915 CURY = (CY - OFFSET + (BOXH1 * 2) + (BOXH2 * 2) + 2)
916 PAINT (CX - R2 - 2, CURY + (BOXH3 / 2)), LIGHTRED, BLACK ' OUTER LEFT EDGE
917 PAINT (CX + R2 + 2, CURY + (BOXH3 / 2)), LIGHTRED, BLACK ' OUTER RIGHT EDGE
918 PAINT (CX - R3 - 2, CURY + (BOXH3 / 2)), YELLOW, BLACK ' INNER LEFT EDGE
919 PAINT (CX + R3 + 2, CURY + (BOXH3 / 2)), YELLOW, BLACK ' INNER RIGHT EDGE
920 CURY = CURY + BOXH3
921 PAINT (CX - R2, CURY + (BOXH4 / 2)), DARKGREY, BLACK
922 PAINT (CX + R2, CURY + (BOXH4 / 2)), DARKGREY, BLACK
923 CURY = CURY + BOXH4
924 PAINT (CX - R2, CURY + (BOXH5 / 2)), GREY, BLACK ' LEFT FOOT
925 PAINT (CX + R2, CURY + (BOXH5 / 2)), GREY, BLACK ' RIGHT FOOT
926 PAINT (CX, CURY + (BOXH6 / 2)), GREY, BLACK ' BOTTOM
927 PAINT (CX - R4 - (W1 / 2) + 2, CY - OFFSET + (BOXH12 / 2)), GREY, BLACK ' LEFT-MOST BOX BELOW JUKEBOX WINDOW
928 PAINT (CX, CY - OFFSET + (BOXH12 / 2)), GREY, BLACK ' MIDDLE BOX BELOW JUKEBOX WINDOW
929 PAINT (CX + R4 + (W1 / 2) - 2, CY - OFFSET + (BOXH12 / 2)), GREY, BLACK ' RIGHT-MOST BOX BELOW JUKEBOX WINDOW
930 PAINT (CX - W1, CY - OFFSET + (BOXH12 / 2)), YELLOW, BLACK ' LEFT YELLLOW BOX BELOW JUKEBOX WINDOW
931 PAINT (CX + W1, CY - OFFSET + (BOXH12 / 2)), YELLOW, BLACK ' RIGHT YELLOW BOX BELOW THE JUKEBOX WINDOW
932 PAINT (CX, CY - OFFSET + BOXH12 + 4 + 3 + 6), LIGHTGREEN, BLACK ' DISPLAY PANEL
933 PAINT (CX - R4 + 4, CY - OFFSET + BOXH12 + 4 + 3), GREY, BLACK ' BUTTON PANEL
934 FOR I = -4 TO 4
935   PAINT(CX + (BTND * I), CY - OFFSET + BOXH12 + 4 + 3 + DISPLAYH + BTNR + 2), RED, BLACK ' BUTTONS
936 NEXT I
937 PAINT (CX, BTNPANELY + BOXH12 + 4), BROWN, BLACK ' SPEAKER
938 PAINT (CX - R7 - 6, BTNPANELY + BOXH12 + 4), LIGHTRED, BLACK ' LEFT SPEAKER FRAME
939 PAINT (CX + R7 + 6, BTNPANELY + BOXH12 + 4), YELLOW, BLACK ' RIGHT SPEAKER FRAME
940 PAINT (CX - R7 - 6, SPEAKERY + BOXH7 + 10), LIGHTRED, BLACK ' BOTTOM LEFT ARC SPEAKER FRAME
941 PAINT (CX + R7 + 6, SPEAKERY + BOXH7 + 10), YELLOW, BLACK ' BOTTOM RIGHT ARC SPEAKER FRAME
942 PAINT (CX - R7 - 6, SPEAKERY + BOXH7 + 3), GREY, BLACK
943 PAINT (CX + R7 + 6, SPEAKERY + BOXH7 + 3), GREY, BLACK
944 PAINT (CX, SPEAKERY + BOXH7 + 6 + R7 + 3), GREY, BLACK

945 ' Draw speaker lines
946 DX = 1
947 DY = 1.5
948 CURSLY = SPEAKERY

949 CURSLX = CX + 24 ' Draw first line (and mirror image) from the top right corner
952 LINXD = ((CX + 3 + R7) - CURSLX) * 1.5
953 LINE (CURSLX, SPEAKERY)-(CX + 3 + R7, SPEAKERY + LINXD), BLACK
954 LINE (CURSLX - ((CURSLX - CX)*2), SPEAKERY)-(CX - 3 - R7, SPEAKERY + LINXD), BLACK

955 CURSLX = CURSLX - 18 ' Draw second line (and mirror image)
956 LINXD = ((CX + 3 + R7) - CURSLX) * 1.5
957 LINE (CURSLX, SPEAKERY)-(CX + 3 + R7, SPEAKERY + LINXD), BLACK
958 LINE (CURSLX - ((CURSLX - CX)*2), SPEAKERY)-(CX - 3 - R7, SPEAKERY + LINXD), BLACK

959 XC = CX + 3: YC = SPEAKERY + BOXH7 + 6 ' Get the center point of the right circle

960 CURSLX = CURSLX - 18 ' Draw third line (and mirror image)
961 LINESX = CURSLX
962 WHILE (CURSLX - XC)^2 + (CURSLY - YC)^2 >= R7^2 ' Find FIRST intersection with the circle
963   CURSLX = CURSLX + DX
964   CURSLY = CURSLY + DY
965 WEND
966 'XENTRY = CURSLX
967 'YENTRY = CURSLY
968 ' Find SECOND intersection
969 WHILE (CURSLX - XC)^2 + (CURSLY - YC)^2 < R7^2
970   CURSLX = CURSLX + DX
971   CURSLY = CURSLY + DY
972 WEND
973 LINE (LINESX, SPEAKERY)-(CURSLX - DX, CURSLY - DY), BLACK
974 LINE (LINESX - ((LINESX - CX)*2), SPEAKERY)-(CURSLX - ((CURSLX - CX)*2) + DX, CURSLY - DY), BLACK

975 CURSLX = LINESX - 18 ' Draw fourth line (and mirror image)
976 CURSLY = SPEAKERY
977 LINESX2 = CURSLX
978 WHILE (CURSLX - XC)^2 + (CURSLY - YC)^2 >= R7^2 ' Find FIRST intersection with the circle
979   CURSLX = CURSLX + DX
980   CURSLY = CURSLY + DY
981 WEND
982 'XENTRY = CURSLX
983 'YENTRY = CURSLY
984 ' Find SECOND intersection
985 WHILE (CURSLX - XC)^2 + (CURSLY - YC)^2 < R7^2
986   CURSLX = CURSLX + DX
987   CURSLY = CURSLY + DY
988 WEND
989 LINE (LINESX2, SPEAKERY)-(CURSLX - DX, CURSLY - DY), BLACK
990 LINE (LINESX2 - ((LINESX2 - CX)*2), SPEAKERY)-(CURSLX - ((CURSLX - CX)*2) + DX, CURSLY - DY), BLACK

991 CURSLX = LINESX2 - 18 ' Draw fifth line (and mirror image)
992 CURSLY = SPEAKERY
993 LINESX3 = CURSLX
994 WHILE (CURSLX - XC)^2 + (CURSLY - YC)^2 >= R7^2 ' Find FIRST intersection with the circle
995   CURSLX = CURSLX + DX
996   CURSLY = CURSLY + DY
997 WEND
998 'XENTRY = CURSLX
999 'YENTRY = CURSLY
1000 ' Find SECOND intersection
1001 WHILE (CURSLX - XC)^2 + (CURSLY - YC)^2 < R7^2
1002   CURSLX = CURSLX + DX
1003   CURSLY = CURSLY + DY
1004 WEND
1005 LINE (CX - R7 - 3, SPEAKERY + 22)-(CURSLX - DX, CURSLY - DY), BLACK ' SPEAKERY + 22 is just an educated guess from trial and error, I didn't want to waste time calculating this precisely
1006 LINE (CX + R7 + 3, SPEAKERY + 22)-(CURSLX - ((CURSLX - CX)*2) + DX, CURSLY - DY), BLACK

1007 ' Draw the sixth line. This one intersects the left circle rather than the right circle
1008 XC = CX - 3 ' So we use the center of the left circle this time
1009 CURSLX = LINESX3 - 18
1010 CURSLY = SPEAKERY
1011 WHILE (CURSLX - XC)^2 + (CURSLY - YC)^2 >= R7^2 ' Find FIRST intersection with the circle
1012   CURSLX = CURSLX + DX
1013   CURSLY = CURSLY + DY
1014 WEND
1015 'XENTRY = CURSLX
1016 'YENTRY = CURSLY
1017 ' Find SECOND intersection
1018 WHILE (CURSLX - XC)^2 + (CURSLY - YC)^2 < R7^2
1019   CURSLX = CURSLX + DX
1020   CURSLY = CURSLY + DY
1021 WEND
1022 LINE (CX - 1 - R7, SPEAKERY + BOXH7 + 3)-(CURSLX - DX, CURSLY - DY), BLACK
1023 LINE (CX + 1 + R7, SPEAKERY + BOXH7 + 3)-(CURSLX - ((CURSLX - CX)*2) + DX, CURSLY - DY), BLACK

1024 LOCATE 3, 58, 0
1025 PRINT "1. CLEMENTINE"
1026 LOCATE 5, 58, 0
1027 PRINT "2. OH! SUSANNA"
1028 LOCATE 7, 58, 0
1029 PRINT "3. COCKLES AND MUSSELS"
1030 LOCATE 9, 58, 0
1031 PRINT "4. DRUNKEN SAILOR"
1032 LOCATE 11, 58, 0
1034 PRINT "5. KUMBAYA"
1035 LOCATE 13, 58, 0
1036 PRINT "6. BLOWING IN THE WIND"
1037 LOCATE 15, 58, 0
1038 PRINT "7. LA BOHEME"

1973 LOCATE 1, 8, 0
1974 PRINT "Press a key from 1 to 7 for a song, any other key to exit..."
1975 K$ = INKEY$
1976 IF K$ = "" THEN GOTO 1975
1977 IF K$ < "1" OR K$ > "7" THEN GOTO 1986
1978 N = VAL(K$) ' Convert key to a number
1979 ON N GOSUB 2000, 2100, 2200, 2300, 2400, 2500, 2600
1980 GOTO 1975

1986 CLS
1987 END

1988 Q = 0
1989 IF I > 0 THEN Q = I / 2
1990 LNX = (Q * BOXH1) + (Q * BOXH2)
1991 LINE (CX - R1 - 5, CY - OFFSET + LNX)-(CX - R3 + 5, CY - OFFSET + LNX + BOXH1), BLACK, B
1992 LINE (CX + R1 + 5, CY - OFFSET + LNX)-(CX + R3 - 5, CY - OFFSET + LNX + BOXH1), BLACK, B
1993 RETURN

1994 Q = 0
1995 IF I > 1 THEN Q = I / 3
1996 LNX = (Q * BOXH2) + ((Q+1) * BOXH1)
1997 LINE (CX - R1 - 3, CY - OFFSET + LNX)-(CX - R3 + 3, CY - OFFSET + LNX + BOXH2), BLACK, B
1998 LINE (CX + R1 + 3, CY - OFFSET + LNX)-(CX + R3 - 3, CY - OFFSET + LNX + BOXH2), BLACK, B
1999 RETURN


2000 REM CLEMENTINE P.39
2010 A1$="T120MSF8.F16F4C4A8.A16A4F4F8A8O5C4.C8O4B-8A8G2G8A8"
2020 A2$="B-4B-4A8G8A4F4F8A8G4.C8E8G8F2F8.F16"
2030 A3$="F4C4A8.A16A4F4F8A8O5C4.C8O4B-8A8G2G8A8"
2040 A4$="B-4B-4A8G8A4F4F8A8G4.C8E8G8F2"
2050 PLAY A1$+A2$+A3$+A4$
2060 RETURN

2100 REM OH! SUSANNA P.37
2110 A1$="T220MLC8E8MSG4G4G4.A8G4E4C4.D8E4E4D4C4D2.MLC8E8MS"
2120 A2$="G4G4G4.A8G4E4MLC4.E8MSE4E4D4D4C2.MLC8E8MS"
2130 A3$="G4G4G4.A8G4E4C4.E8E4E4D4C4D2.MLC8E8MS"
2140 A4$="G4G4G4.A8G4E4C4.E8E4E4D4D4C1"
2150 A5$="F2F2A4A2A4G4G4E4C4D2MLC8E8MS"
2160 A6$="G4G4G4.A8G4E4C4.E8E4E4D4D4C2."
2170 PLAY A1$+A2$+A3$+A4$+A5$+A6$
2180 RETURN

2200 REM COCKLES AND MUSSELS (MOLLY MALONE)
2210 A1$="T110FFAF8F8GGGGB-.G8"
2220 A2$="AGFO5CO4B-AAG.F8G2C8C8"
2230 A3$="FFFFA.F8GGGGB-.G8"
2240 A4$="AO5C.O4B-8AO5C.O4B-8AFGF2C"
2250 A5$="FFFMLFA.MSF8GGGMLGB-.MSG8"
2260 A6$="AO5C.O4B-8AO5C.O4B-8AFGF2"
2270 PLAY A1$+A2$+A3$+A4$+A5$+A6$
2280 RETURN

2300 REM DRUNKEN SAILOR
2310 A1$="AA8A8AA8A8ADFAGG8G8GG8G8"
2320 A2$="GCEGAA8A8AA8A8ABO5CDCO4AGES2SP4"
2330 A2$="GCEGAA8A8AA8A8ABO5CDCO4AGED2DP4"
2340 A3$="A2A.A8ADFAG2G.G8GCEG"
2350 A4$="A2A.A8ABO5CDCO4AGED2DP4"
2360 PLAY A1$+A2$+A3$+A4$
2370 RETURN

2400 REM KUMBAYA P.55
2410 A1$="T70C8E8G8.G16GA8A8G2C8E8G8.G16GF8E8D2C8E8"
2420 A2$="G8.G16GA8A8G2FMLE16.D16.C16.C8.MSD8D8C2"
2430 PLAY A1$+A2$
2440 RETURN

2500 REM BLOWING IN THE WIND P.29
2510 A1$="T150G2GGA2GFG2MLEDMSC2.E"
2520 A2$="G2GGA2GFMLG1G2MSEF"
2530 A3$="MLG2GMSEF2FEMLD1D2MSEF"
2540 A4$="G2GGA2GFG8G.MLEDMSC2.E"
2550 A5$="G2GGA2GFMLG1G2.MSE"
2560 A6$="F2FED2.DE8E.EDC2.E"
2570 A7$="F2FEDDCO3BO4MLC1C2.MS"
2580 PLAY A1$+A2$+A1$+A3$+A4$+A5$+A6$+A7$+A8$
2590 RETURN

2600 REM LA BOHEME
2610 A1$="F8B-8O4BD8F.F.E-8E-8E-8E-O4B-8O5D.DD8C.C8CO4G8B-.B-.B-."
2620 A2$="O5D8P8P8D8F2E-.E-8O4B-B-8B-8O5C8D8E-8C8DDO4B-8O5C8D8O4B-8O5C2CP4"
2630 PLAY A1$+A2$
2640 RETURN

